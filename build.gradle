plugins {
    id 'java'
    id 'maven-publish'
}

def targetJavaVersion = 21

def isSnapshot = providers.environmentVariable("SNAPSHOT_BUILD").map { it.toBoolean() }.orElse(false).get()
def buildNumber = System.getenv('BUILD_NUMBER') ?: 'dev'

group = maven_group
version = "${project_version}" + (isSnapshot ? "-SNAPSHOT+${buildNumber}" : "")

base {
    archivesName = archives_base_name
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains:annotations:26.0.2'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = artifact_id
        }
    }
    repositories {
        maven {
            url = uri(providers.gradleProperty("mavenRepositoryUrl").orNull ?: "http://localhost:8081/releases/nl/devpieter/sees")
            credentials {
                username = providers.gradleProperty("mavenRepositoryUsername").orNull
                password = providers.gradleProperty("mavenRepositoryPassword").orNull
            }
        }
    }
}
