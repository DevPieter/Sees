plugins {
    id 'java'
    id 'maven-publish'
}

def targetJavaVersion = 21

def hash = System.getenv("GITHUB_SHA")?.substring(0, 7) ?: "dev"
def isSnapshot = System.getenv('SNAPSHOT_BUILD')?.toBoolean() ?: false

group = maven_group
archivesBaseName = archives_base_name
version = "${project_version}" + (isSnapshot ? "-${hash}-SNAPSHOT" : "")

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.11.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation 'org.jetbrains:annotations:26.0.2'
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release.set(targetJavaVersion)
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = artifact_id
        }
    }
    repositories {
        maven {
            url = findProperty('mavenRepositoryUrl')
            credentials {
                username = findProperty('mavenRepositoryUsername')
                password = findProperty('mavenRepositoryPassword')
            }
        }
    }
}
